**向规则的事物中添加一些“杂乱无章”（噪音）的效果**往往会有意想不到的效果。

这些噪声纹理可以被认为是一种**程序纹理（Procedure Texture）**，由计算机利用某些算法生成。

**Perlin噪声**（生成更自然的噪声纹理）和**Worley噪声**（模拟诸如石头，水，纸张等多孔噪声）是两种最常使用的噪声类型。

现代的**图像编辑软件（PhotoShop等）**提供类似的功能或插件，以帮助美术人员生成需要的噪声纹理，如果想要更加自由地控制噪声纹理的生成，需要了解它们的生成原理。

本章学习如何使用噪声来模拟各种看似“神奇”的特效。

# 消融效果

**消融（dissolve）效果**常见于游戏中的**角色死亡**、**地图烧毁**等效果。

这些效果中，消融往往从不同的区域开始，并向看似随机的方向扩张，最后整个物体都将消失不见。

**原理：**噪声纹理+透明度测试。使用**对噪声纹理采样的结果和某个控制消融程度的阈值比较**，如果小于阈值，则使用`clip`函数把它对应的像素裁剪掉。这些部分对应**被烧毁的区域**。而镂空区域边缘的烧焦效果则是将两种颜色混合，再用`pow`函数处理后，与原纹理颜色混合后的结果。

1. Unity中新建一个场景，默认情况下场景包含一个摄像机和一个平行光，并且使用了内置的天空盒子。在`Window->Lighting->Skybox`中去掉场景中的天空盒子。
2. 新建一个材质，名为`DissolveMat`。
3. 新建一个Unity Shader，名为`Dissolve`，把新的Unity Shader赋给第2步中创建的材质。
4. 搭建一个**测试消融的场景**，构建一个包含3面墙的房间，并且放置了一个立方体。把第2步创建的材质拖曳给立方体。
5. 保存场景。

编写`Dissolve`：

```c++
// Upgrade NOTE: replaced '_Object2World' with 'unity_ObjectToWorld'
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 15/Dissolve" {
	Properties {
		// _BurnAmount用于控制消融程度,当值为0时,物体为正常效果,值为1时,物体完全消融
		_BurnAmount ("Burn Amount", Range(0.0, 1.0)) = 0.0
		// _LineWidth控制模拟烧焦效果时的线宽,值越大,火焰蔓延的范围越广
		_LineWidth("Burn Line Width", Range(0.0, 0.2)) = 0.1
		// _MainTex物体原本的漫反射纹理
		_MainTex ("Base (RGB)", 2D) = "white" {}
		// _BumpMap物体原本的法线纹理
		_BumpMap ("Normal Map", 2D) = "bump" {}
		// _BurnFirstColor和_BurnSecondColor火焰边缘的两种颜色
		_BurnFirstColor("Burn First Color", Color) = (1, 0, 0, 1)
		_BurnSecondColor("Burn Second Color", Color) = (1, 0, 0, 1)
		// _BurnMap关键的噪声处理
		_BurnMap("Burn Map", 2D) = "white"{}
	}

	SubShader {
		Tags { "RenderType"="Opaque" "Queue"="Geometry"}
		
		// 定义消融所需的Pass
		Pass {
			// 设置Pass的LightMode和multi_compile_fwdbase编译指令
			Tags { "LightMode"="ForwardBase" }

			// 使用Cull关闭该Shader的面片剔除
			// 即模型的正面和背面都会被渲染
			// 这是因为消融会导致裸露模型内部的构造,如果只渲染正面会出现错误的结果
			Cull Off
			
			CGPROGRAM
			
			#include "Lighting.cginc"
			#include "AutoLight.cginc"
			
			#pragma multi_compile_fwdbase
			
			#pragma vertex vert
			#pragma fragment frag
			
			fixed _BurnAmount;
			fixed _LineWidth;
			sampler2D _MainTex;
			sampler2D _BumpMap;
			fixed4 _BurnFirstColor;
			fixed4 _BurnSecondColor;
			sampler2D _BurnMap;
			
			float4 _MainTex_ST;
			float4 _BumpMap_ST;
			float4 _BurnMap_ST;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 tangent : TANGENT;
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float2 uvMainTex : TEXCOORD0;
				float2 uvBumpMap : TEXCOORD1;
				float2 uvBurnMap : TEXCOORD2;
				float3 lightDir : TEXCOORD3;
				float3 worldPos : TEXCOORD4;
				SHADOW_COORDS(5)
			};
			
			v2f vert(a2v v) {
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				
				// 使用宏TRANSFORM_TEX计算三张纹理对应的纹理坐标
				o.uvMainTex = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uvBumpMap = TRANSFORM_TEX(v.texcoord, _BumpMap);
				o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);
				
				TANGENT_SPACE_ROTATION;
				// 把光源方向从模型空间变换到切线空间
  				o.lightDir = mul(rotation, ObjSpaceLightDir(v.vertex)).xyz;
  				
				// 为了得到阴影信息,计算世界空间下的顶点位置和阴影纹理的采样坐标(使用TRANSFER_SHADOW)
  				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
  				
  				TRANSFER_SHADOW(o);
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				// 对噪声纹理进行采样
				fixed3 burn = tex2D(_BurnMap, i.uvBurnMap).rgb;
				
				// 将采样结果与用于控制消融程度的属性_BurnAmount相减,传递给clip函数
				// 当结果小于0时,该像素会被剔除,从而不会显示在屏幕上
				// 如果通过测试,则进行正常光照计算
				clip(burn.r - _BurnAmount);
				
				float3 tangentLightDir = normalize(i.lightDir);
				fixed3 tangentNormal = UnpackNormal(tex2D(_BumpMap, i.uvBumpMap));
				
				// 根据漫反射纹理得到材质的反射率albedo
				fixed3 albedo = tex2D(_MainTex, i.uvMainTex).rgb;
				
				// 由此计算得到环境光
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;
				
				// 进而得到漫反射光照
				fixed3 diffuse = _LightColor0.rgb * albedo * max(0, dot(tangentNormal, tangentLightDir));

				// 计算烧焦颜色burnColor
				// 在宽度为_LineWidth的范围内模拟一个烧焦的颜色变化
				// 第一步使用smoothstep函数计算混合系数t
				// 当t值为1时,表明像素位于消融的边界处
				// 当t值为0时,表明该像素为正常的模型颜色
				// 而中间的插值则表示需要模拟一个烧焦效果
				fixed t = 1 - smoothstep(0.0, _LineWidth, burn.r - _BurnAmount);
				// 使用t来混合两种火焰颜色_BurnFirstColor和_BurnSecondColor
				fixed3 burnColor = lerp(_BurnFirstColor, _BurnSecondColor, t);
				// 为了让效果更接近烧焦的痕迹,使用pow函数对结果进行处理
				burnColor = pow(burnColor, 5);
				
				UNITY_LIGHT_ATTENUATION(atten, i, i.worldPos);
				// 再次使用t来混合正常的光照颜色(环境光+漫反射)和烧焦颜色
				// 使用step函数来保证当_BurnAmount为0时,不显示任何消融效果
				fixed3 finalColor = lerp(ambient + diffuse * atten, burnColor, t * step(0.0001, _BurnAmount));
				
				// 返回混合后的颜色值finalColor
				return fixed4(finalColor, 1);
			}
			
			ENDCG
		}
		
		// Pass to render object as a shadow caster
		// 定义一个投射阴影的Pass
		// 使用透明度测试的物体的阴影需要特别处理
		// 如果仍然使用普通的阴影Pass,则被剔除的区域仍然会向其他物体投射阴影,造成穿帮
		// 阴影投射的重点在于需要按正常Pass处理来剔除片元或进行顶点动画,以便阴影可以和物体正常渲染的结果相匹配
		Pass {
			// Unity中用于投射阴影的Pass的LightMode需要被设置为ShadowCaster
			Tags { "LightMode" = "ShadowCaster" }
			
			CGPROGRAM
			
			#pragma vertex vert
			#pragma fragment frag
			
			// 同时需要使用#pragma multi_compile_shadowcaster指明它需要的编译指令
			#pragma multi_compile_shadowcaster
			
			#include "UnityCG.cginc"
			
			fixed _BurnAmount;
			sampler2D _BurnMap;
			float4 _BurnMap_ST;
			
			struct v2f {
				// 使用宏V2F_SHADOW_CASTER,TRANSFER_SHADOW_CASTER_NORMALOFFSET和SHADOW_CASTER_FRAGMENT
				// 来帮助计算阴影投射时需要的各种变量,而我们可以只关注自定义计算的部分
				// 使用V2F_SHADOW_CASTER定义阴影投射需要定义的变量
				V2F_SHADOW_CASTER;
				float2 uvBurnMap : TEXCOORD1;
			};
			
			v2f vert(appdata_base v) {
				v2f o;
				
				// 使用TRANSFER_SHADOW_CASTER_NORMALOFFSET来填充V2F_SHADOW_CASTER在背后声明的一些变量
				// 此步骤由Unity在背后为我们完成
				TRANSFER_SHADOW_CASTER_NORMALOFFSET(o)
				
				// 计算噪声纹理的采样结果来剔除片元
				o.uvBurnMap = TRANSFORM_TEX(v.texcoord, _BurnMap);
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				fixed3 burn = tex2D(_BurnMap, i.uvBurnMap).rgb;
				
				clip(burn.r - _BurnAmount);
				
				// 再利用SHADOW_CASTER_FRAGMENT让Unity完成阴影投射部分,把结果输出到深度图和阴影映射纹理中
				SHADOW_CASTER_FRAGMENT(i)
			}
			ENDCG
		}
	}
	FallBack "Diffuse"
}
```

把噪声纹理`Burn_Noise.png`拖曳到材质的`_BurnMap`属性上，再调整材质的`_BurnAmount`属性，就可以看到木箱逐渐消融的效果。

<img src="./images/143.gif"  style="zoom:40%;" />

使用不同的噪声和纹理属性（即材质面板上**纹理的Tiling和Offset值**）都会得到不同的消融效果。

因此，想要得到好的消融效果，也需要美术人员提供合适的噪声纹理来配合。



# 水波效果

**模拟实时水面**的过程中，往往也会使用噪声纹理。

此时**噪声纹理通常会用作一个高度图**，以不断修改水面的法线方向。为了模拟水不断流动的效果，会使用和时间相关的变量来对噪声进行采样，当**得到法线信息后**，再进行**正常的反射+折射计算**，得到最后的水面波动效果。

使用一个**由噪声纹理得到的法线贴图**，实现一个**包含菲涅尔反射的水面效果**。

使用一张**立方体纹理（Cubemap）**作为环境纹理，模拟反射。为了模拟折射效果，使用**GrabPass**获取当前屏幕的渲染纹理，并使用**切线空间下的法线方向对像素的屏幕进行偏移**，再使用**该坐标对渲染纹理进行屏幕采样**，从而模拟近似的折射效果。

水波的**法线纹理由一张噪声纹理生成而得**，随着时间的变化**不断平移**，模拟波光粼粼的效果。

除此之外，没有使用一个定值来混合反射和折射颜色，而是使用之前的菲涅尔系数来动态决定混合系数。

计算**菲涅尔系数**：
$$
fresnel = pow(1 - max(0, v\cdot n), 4)	\tag{1}
$$
其中，$v$和$n$分别对应了**视角和法线方向**。它们之间的夹角越小，$fresnel$值越小，反射越弱，折射越强。菲涅尔系数经常用于**边缘光照**的计算中。

1. Unity中新建一个场景，默认情况下场景包含一个摄像机和一个平行光，并且使用了内置的天空盒子。在`Window->Lighting->Skybox`中去掉场景中的天空盒子。
2. 新建一个材质，名为`WaterWaveMat`。
3. 新建一个Unity Shader，名为`WaterWave`，把新的Unity Shader赋给第2步中创建的材质。
4. 搭建一个**测试水波效果的场景**，构建一个由6面墙围成的封闭房间，都使用标准材质，在房间中放置一个平面来模拟水面。把第2步创建的材质拖曳给该平面。
5. 为了得到场景使用的环境纹理，使用第10章实现的创建立方体纹理的脚本（通过`Gameobject->Render into Cubemap`打开编辑窗口）来创建它。
6. 保存场景。

编写`WaterWave`：

```c++
// Upgrade NOTE: replaced '_Object2World' with 'unity_ObjectToWorld'
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 15/Water Wave" {
	Properties {
		// _Color用于控制水面颜色
		_Color ("Main Color", Color) = (0, 0.15, 0.115, 1)
		// _MainTex水面波纹材质纹理
		_MainTex ("Base (RGB)", 2D) = "white" {}
		// _WaveMap由噪声纹理生成的法线纹理
		_WaveMap ("Wave Map", 2D) = "bump" {}
		// _Cubemap模拟反射的立方体纹理
		_Cubemap ("Environment Cubemap", Cube) = "_Skybox" {}
		// _WaveXSpeed和_WaveYSpeed控制法线纹理在X和Y方向上的平移速度
		_WaveXSpeed ("Wave Horizontal Speed", Range(-0.1, 0.1)) = 0.01
		_WaveYSpeed ("Wave Vertical Speed", Range(-0.1, 0.1)) = 0.01
		// _Distortion用于控制模拟折射时图像的扭曲程度
		_Distortion ("Distortion", Range(0, 100)) = 10
	}
	SubShader {
		// We must be transparent, so other objects are drawn before this one.
		// 定义相应的渲染队列
		// 将渲染队列设置成Transparent(可以确保该物体渲染时,其他所有不透明物体都已被渲染到屏幕上了)
		// 把RenderType设为Opaque,为了在使用着色器替换(Shader Replacement时),该物体可以在需要时被正确渲染
		// 这通常发生在需要得到摄像机的深度和法线纹理时
		Tags { "Queue"="Transparent" "RenderType"="Opaque" }
		
		// This pass grabs the screen behind the object into a texture.
		// We can access the result in the next pass as _RefractionTex
		// 使用GrabPass获取屏幕图像的Pass
		// 该Pass中定义一个字符串,内部的名称决定了抓取得到的屏幕图像将会被存入哪个纹理中
		GrabPass { "_RefractionTex" }
		
		Pass {
			Tags { "LightMode"="ForwardBase" }
			
			CGPROGRAM
			
			#include "UnityCG.cginc"
			#include "Lighting.cginc"
			
			#pragma multi_compile_fwdbase
			
			#pragma vertex vert
			#pragma fragment frag
			
			fixed4 _Color;
			sampler2D _MainTex;
			float4 _MainTex_ST;
			sampler2D _WaveMap;
			float4 _WaveMap_ST;
			samplerCUBE _Cubemap;
			fixed _WaveXSpeed;
			fixed _WaveYSpeed;
			float _Distortion;	
			// _RefractionTex和_RefractionTex_TexelSize对应在使用GrabPass时,指定的纹理名称
			sampler2D _RefractionTex;
			// _RefractionTex_TexelSize可以得到该纹理的纹素大小
			// 一个大小为256x512的纹理,其纹素大小为(1/256,1/512)
			// 需要对屏幕图像的采样坐标进行偏移时使用该变量
			float4 _RefractionTex_TexelSize;
			
			struct a2v {
				float4 vertex : POSITION;
				float3 normal : NORMAL;
				float4 tangent : TANGENT; 
				float4 texcoord : TEXCOORD0;
			};
			
			struct v2f {
				float4 pos : SV_POSITION;
				float4 scrPos : TEXCOORD0;
				float4 uv : TEXCOORD1;
				float4 TtoW0 : TEXCOORD2;  
				float4 TtoW1 : TEXCOORD3;  
				float4 TtoW2 : TEXCOORD4; 
			};
			
			v2f vert(a2v v) {
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex);
				
				// 调用ComputeGrabScreenPos得到对应被抓取屏幕图像的采样坐标
				o.scrPos = ComputeGrabScreenPos(o.pos);
				
				// 计算_MainTex和_WaveMap的采样坐标,并把它们分别存储在一个float4类型变量的xy和zw分量中
				o.uv.xy = TRANSFORM_TEX(v.texcoord, _MainTex);
				o.uv.zw = TRANSFORM_TEX(v.texcoord, _WaveMap);
				
				float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;  
				fixed3 worldNormal = UnityObjectToWorldNormal(v.normal);  
				fixed3 worldTangent = UnityObjectToWorldDir(v.tangent.xyz);  
				fixed3 worldBinormal = cross(worldNormal, worldTangent) * v.tangent.w; 
				
				// 由于需要在片元着色器中把法线方向从切线空间(由法线纹理采样得到)变换到世界空间下
				// 以便对Cubemap进行采样
				// 因此需要在这里了计算该顶点对应的从切线空间到世界空间的变换矩阵
				// 并把该矩阵的每一行分别存储在TtoW0,TtoW1和TtoW2的xyz分量中
				// w分量存储世界空间下的顶点坐标
				o.TtoW0 = float4(worldTangent.x, worldBinormal.x, worldNormal.x, worldPos.x);  
				o.TtoW1 = float4(worldTangent.y, worldBinormal.y, worldNormal.y, worldPos.y);  
				o.TtoW2 = float4(worldTangent.z, worldBinormal.z, worldNormal.z, worldPos.z);  
				
				return o;
			}
			
			fixed4 frag(v2f i) : SV_Target {
				// 通过TtoW0的w分量得到世界坐标,并用该值得到该片元对应的视角方向
				float3 worldPos = float3(i.TtoW0.w, i.TtoW1.w, i.TtoW2.w);
				fixed3 viewDir = normalize(UnityWorldSpaceViewDir(worldPos));
				
				// 使用内置的_Time.y变量和_WaveXSpeed,_WaveYSpeed属性计算法线纹理的当前偏移量
				float2 speed = _Time.y * float2(_WaveXSpeed, _WaveYSpeed);
				
				// Get the normal in tangent space
				// 并利用该值对法线纹理进行两次采样(模拟两层交叉的水面波动的效果)
				fixed3 bump1 = UnpackNormal(tex2D(_WaveMap, i.uv.zw + speed)).rgb;
				fixed3 bump2 = UnpackNormal(tex2D(_WaveMap, i.uv.zw - speed)).rgb;
				// 对两次结果相加并归一化后得到切线空间下的法线方向
				fixed3 bump = normalize(bump1 + bump2);
				
				// Compute the offset in tangent space
				// 使用该值和_Distortion属性以及_RefractionTex_TexelSize来对屏幕图像的采样坐标进行偏移,模拟折射效果
				// _Distortion值越大,偏移量越大,水面背后的物体看起来变形程度越大
				float2 offset = bump.xy * _Distortion * _RefractionTex_TexelSize.xy;
				// 此处选择切线空间下的法线方向来进行偏移
				// 因为该空间下的法线可以反映顶点局部空间下的法线方向
				// 计算偏移后的屏幕坐标时,把偏移量和屏幕坐标的z分量相乘,为了模拟深度越大,折射程度越大的效果
				i.scrPos.xy = offset * i.scrPos.z + i.scrPos.xy;
				// 对scrPos进行透视除法,再使用该坐标对抓取的屏幕图像_RefractionTex进行采样,得到模拟的折射颜色
				fixed3 refrCol = tex2D( _RefractionTex, i.scrPos.xy/i.scrPos.w).rgb;
				
				// Convert the normal to world space
				// 之后把法线方向从切线空间变换到世界空间下
				// 并据此得到视角方向相对于法线方向的反射方向
				bump = normalize(half3(dot(i.TtoW0.xyz, bump), dot(i.TtoW1.xyz, bump), dot(i.TtoW2.xyz, bump)));
				// 使用反射方向对Cubemap进行采样
				fixed4 texColor = tex2D(_MainTex, i.uv.xy + speed);
				fixed3 reflDir = reflect(-viewDir, bump);
				// 并把结果和主纹理颜色相乘后得到反射颜色.也对主纹理进行了纹理动画,以模拟水波效果
				fixed3 reflCol = texCUBE(_Cubemap, reflDir).rgb * texColor.rgb * _Color.rgb;
				
				// 为了混合折射和反射颜色,计算菲涅尔系数
				// 并据此来混合折射和反射颜色,作为最终输出的颜色
				fixed fresnel = pow(1 - saturate(dot(viewDir, bump)), 4);
				fixed3 finalColor = reflCol * fresnel + refrCol * (1 - fresnel);
				
				return fixed4(finalColor, 1);
			}
			
			ENDCG
		}
	}
	// Do not cast shadow
	FallBack Off
}
```

使用噪声纹理`Water_Noise.png`。

本例中需要的是一张法线纹理，因此可以从该噪声纹理的灰度值中生成需要的法线信息，通过在它的纹理面板中把纹理类型设置为**Normal map**，并选中**Create from grayscale**来完成的。

最后把生成的法线纹理拖曳到材质的`_WaveMap`属性上，再点击运行后，可以看到水面波动的效果了。

<img src="./images/144.gif"  style="zoom:40%;" />



# 全局雾效

**基于屏幕后处理的全局雾效**：由深度纹理重建每个像素在世界空间下的位置，再使用一个基于高度的公式来计算雾效的混合系数，最后使用该系数来混合雾的颜色和原屏幕颜色。（**基于高度的均匀雾效**，即在同一个高度上，雾的浓度是相同的）

有些时候希望可以模拟一种不均匀的雾效，同时让雾不断飘动，**使雾看起来更加缥缈**，此时可以通过使用一张噪声纹理来实现。

1. Unity中新建一个场景，默认情况下场景包含一个摄像机和一个平行光，并且使用了内置的天空盒子。在`Window->Lighting->Skybox`中去掉场景中的天空盒子。
2. 搭建一个**测试雾效的场景**，构建一个包含3面墙的房间，并放置两个立方体和两个球体。都使用标准材质。
3. 新建一个脚本，名为`FogWithNoise.cs`，并把该脚本拖曳到摄像机上。
4. 新建一个Unity Shader，名为`FogWithNoise`。
5. 保存场景。

编写`FogWithNoise.cs`：

```c#
using UnityEngine;
using System.Collections;

public class FogWithNoise : PostEffectsBase {

	// 声明需要的Shader,并据此创建相应的材质
	public Shader fogShader;
	private Material fogMaterial = null;

	public Material material {  
		get {
			fogMaterial = CheckShaderAndCreateMaterial(fogShader, fogMaterial);
			return fogMaterial;
		}  
	}
	
	// 需要获取摄像机的相关参数,如近裁剪平面的距离,FOV等
	// 同时还需要获取摄像机在世界空间下的前方,上方和右方等方向
	// 因此用两个变量存储摄像机的Camera组件和Transform组件
	private Camera myCamera;
	public Camera camera {
		get {
			if (myCamera == null) {
				myCamera = GetComponent<Camera>();
			}
			return myCamera;
		}
	}

	private Transform myCameraTransform;
	public Transform cameraTransform {
		get {
			if (myCameraTransform == null) {
				myCameraTransform = camera.transform;
			}
			
			return myCameraTransform;
		}
	}

	// 定义模拟雾效时的各个参数
	[Range(0.1f, 3.0f)]
	// fogDensity用于控制雾的浓度
	public float fogDensity = 1.0f;

	// fogColor控制雾的颜色
	public Color fogColor = Color.white;

	// 由于使用的雾效控制函数是基于高度的,因此参数fogStart用于控制雾效的起始高度
	public float fogStart = 0.0f;
	// fogEnd用于控制雾效的终止高度
	public float fogEnd = 2.0f;

	// noiseTexture使用的噪声纹理
	public Texture noiseTexture;

	// fogXSpeed和fogYSpeed分别对应噪声纹理在X和Y方向上的移动速度
	// 以此来模拟雾的飘动效果
	[Range(-0.5f, 0.5f)]
	public float fogXSpeed = 0.1f;

	[Range(-0.5f, 0.5f)]
	public float fogYSpeed = 0.1f;

	// noiseAmount用于控制噪声程度
	// 当noiseAmount为0时,表示不应用任何噪声,即得到一个均匀的基于高度的全局雾效
	[Range(0.0f, 3.0f)]
	public float noiseAmount = 1.0f;

	void OnEnable() {
		// 由于需要获取摄像机的深度纹理,因此在脚本的OnEnable函数中设置摄像机的相应状态
		GetComponent<Camera>().depthTextureMode |= DepthTextureMode.Depth;
	}
		
	void OnRenderImage (RenderTexture src, RenderTexture dest) {
		if (material != null) {
			Matrix4x4 frustumCorners = Matrix4x4.identity;
			
			float fov = camera.fieldOfView;
			float near = camera.nearClipPlane;
			float aspect = camera.aspect;
			
			float halfHeight = near * Mathf.Tan(fov * 0.5f * Mathf.Deg2Rad);
			Vector3 toRight = cameraTransform.right * halfHeight * aspect;
			Vector3 toTop = cameraTransform.up * halfHeight;
			
			// 计算近裁剪平面四个角对应的向量
			// 并把它们存储在一个矩阵类型的变量(frustumCorners)中
			Vector3 topLeft = cameraTransform.forward * near + toTop - toRight;
			float scale = topLeft.magnitude / near;
			
			topLeft.Normalize();
			topLeft *= scale;
			
			Vector3 topRight = cameraTransform.forward * near + toRight + toTop;
			topRight.Normalize();
			topRight *= scale;
			
			Vector3 bottomLeft = cameraTransform.forward * near - toTop - toRight;
			bottomLeft.Normalize();
			bottomLeft *= scale;
			
			Vector3 bottomRight = cameraTransform.forward * near + toRight - toTop;
			bottomRight.Normalize();
			bottomRight *= scale;
			
			frustumCorners.SetRow(0, bottomLeft);
			frustumCorners.SetRow(1, bottomRight);
			frustumCorners.SetRow(2, topRight);
			frustumCorners.SetRow(3, topLeft);
			
			material.SetMatrix("_FrustumCornersRay", frustumCorners);

			material.SetFloat("_FogDensity", fogDensity);
			material.SetColor("_FogColor", fogColor);
			material.SetFloat("_FogStart", fogStart);
			material.SetFloat("_FogEnd", fogEnd);

			material.SetTexture("_NoiseTex", noiseTexture);
			material.SetFloat("_FogXSpeed", fogXSpeed);
			material.SetFloat("_FogYSpeed", fogYSpeed);
			material.SetFloat("_NoiseAmount", noiseAmount);

			// 最后把结果和其他参数传递给材质,并调用Graphics.Blit (src, dest, material);把渲染结果显示在屏幕上
			Graphics.Blit (src, dest, material);
		} else {
			Graphics.Blit(src, dest);
		}
	}
}
```

编写`FogWithNoise`：

```c++
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "Unity Shaders Book/Chapter 15/Fog With Noise" {
	Properties {
		_MainTex ("Base (RGB)", 2D) = "white" {}
		_FogDensity ("Fog Density", Float) = 1.0
		_FogColor ("Fog Color", Color) = (1, 1, 1, 1)
		_FogStart ("Fog Start", Float) = 0.0
		_FogEnd ("Fog End", Float) = 1.0
		_NoiseTex ("Noise Texture", 2D) = "white" {}
		_FogXSpeed ("Fog Horizontal Speed", Float) = 0.1
		_FogYSpeed ("Fog Vertical Speed", Float) = 0.1
		_NoiseAmount ("Noise Amount", Float) = 1
	}
	SubShader {
		CGINCLUDE
		
		#include "UnityCG.cginc"
		
		// _FrustumCornersRay虽然没有在Properties中声明
		// 但仍可以由脚本传递给Shader
		float4x4 _FrustumCornersRay;
		
		sampler2D _MainTex;
		half4 _MainTex_TexelSize;
		// 声明深度纹理_CameraDepthTexture
		// Unity会在背后把得到的深度纹理传递给该值
		sampler2D _CameraDepthTexture;
		half _FogDensity;
		fixed4 _FogColor;
		float _FogStart;
		float _FogEnd;
		sampler2D _NoiseTex;
		half _FogXSpeed;
		half _FogYSpeed;
		half _NoiseAmount;
		
		struct v2f {
			float4 pos : SV_POSITION;
			float2 uv : TEXCOORD0;
			float2 uv_depth : TEXCOORD1;
			float4 interpolatedRay : TEXCOORD2;
		};
		
		v2f vert(appdata_img v) {
			v2f o;
			o.pos = UnityObjectToClipPos(v.vertex);
			
			o.uv = v.texcoord;
			o.uv_depth = v.texcoord;
			
			#if UNITY_UV_STARTS_AT_TOP
			if (_MainTex_TexelSize.y < 0)
				o.uv_depth.y = 1 - o.uv_depth.y;
			#endif
			
			int index = 0;
			if (v.texcoord.x < 0.5 && v.texcoord.y < 0.5) {
				index = 0;
			} else if (v.texcoord.x > 0.5 && v.texcoord.y < 0.5) {
				index = 1;
			} else if (v.texcoord.x > 0.5 && v.texcoord.y > 0.5) {
				index = 2;
			} else {
				index = 3;
			}
			#if UNITY_UV_STARTS_AT_TOP
			if (_MainTex_TexelSize.y < 0)
				index = 3 - index;
			#endif
			
			o.interpolatedRay = _FrustumCornersRay[index];
				 	 
			return o;
		}
		
		fixed4 frag(v2f i) : SV_Target {
			// 根据深度纹理重建该像素在世界空间中的位置
			float linearDepth = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE(_CameraDepthTexture, i.uv_depth));
			float3 worldPos = _WorldSpaceCameraPos + linearDepth * i.interpolatedRay.xyz;
			
			// 利用内置的_Time.y变量和_FogXSpeed,_FogYSpeed属性计算出当前噪声纹理的偏移量
			float2 speed = _Time.y * float2(_FogXSpeed, _FogYSpeed);
			// 并据此对噪声纹理进行采样,得到噪声值
			// 把该值减去0.5,再乘以控制噪声程度的属性_NoiseAmount得到最终的噪声值
			float noise = (tex2D(_NoiseTex, i.uv + speed).r - 0.5) * _NoiseAmount;

			float fogDensity = (_FogEnd - worldPos.y) / (_FogEnd - _FogStart); 
			// 把该噪声值添加到雾效浓度的计算中,得到应用噪声后的雾效混合系数fogDensity
			fogDensity = saturate(fogDensity * _FogDensity * (1 + noise));
			
			fixed4 finalColor = tex2D(_MainTex, i.uv);
			// 使用该系数将雾的颜色和原始颜色进行混合后返回
			finalColor.rgb = lerp(finalColor.rgb, _FogColor.rgb, fogDensity);
			
			return finalColor;
		}
		
		ENDCG
		
		// 定义雾效渲染所需的Pass
		Pass {          	
			CGPROGRAM  
			
			#pragma vertex vert  
			#pragma fragment frag  
			  
			ENDCG
		}
	} 
	// 关闭Shader的Fallback
	FallBack Off
}
```

把噪声纹理`FogWithNoise`拖曳到摄像机的`FogWithNoise.cs`脚本中的`fogShader`参数中。得到如下效果：

<img src="./images/145.gif"  style="zoom:40%;" />

